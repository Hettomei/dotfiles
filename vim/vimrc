" be iMproved
set nocompatible

" required!
filetype off

" Vundle {
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()

Plugin 'gmarik/Vundle.vim'
Plugin 'godlygeek/tabular' " Make multiple things aligned
Plugin 'scrooloose/syntastic'
Plugin 'gorkunov/smartpairs.vim'
Plugin 'Konfekt/FastFold' " Fix very slow vim because of foldmethod=syntax

" tpope
Plugin 'tpope/vim-fugitive' " Gblame, Gremove .... fun
Plugin 'tpope/vim-commentary' " use gcc
Plugin 'tpope/vim-rsi' "allow you to use <ctrl-a> as move to left in command mode
Plugin 'tpope/vim-surround' " To remove the delimiters entirely to 'Hello world!' press ds'.  Hello world!. or ysiw( . or visual mode then S(
Plugin 'tpope/vim-eunuch' "Add unix command like :Remove :Move :SudoWrite
Plugin 'tpope/gem-ctags'
Plugin 'tpope/vim-projectionist' " Allow to use :A on any project
Plugin 'tpope/vim-bundler' " add gf on Gemfile to open gem source
Plugin 'tpope/vim-rake' " Need vim-projectionist ta add a :A for alternative file
Plugin 'tpope/vim-rails'
Plugin 'tpope/vim-characterize' " Add more display when press gA on a char
Plugin 'tpope/vim-repeat' " Allow to repeat custom map
Plugin 'tpope/vim-rbenv'

Plugin 'jayflo/vim-skip'
" Plugin 'mileszs/ack.vim'
Plugin 'rking/ag.vim'
Plugin 'edsono/vim-matchit' "Improve math for if ... end when press %
Plugin 'altercation/vim-colors-solarized'

" language specific
Plugin 'gudleik/vim-slim'
Plugin 'pangloss/vim-javascript'
Plugin 'vim-ruby/vim-ruby'
Plugin 'tpope/vim-haml'
Plugin 'tpope/vim-markdown'
Plugin 'kchmck/vim-coffee-script'
" Plugin 'wting/rust.vim'

" for opening file
Plugin 'kien/ctrlp.vim'

" other
Plugin 'msanders/snipmate.vim'
Plugin 'junegunn/goyo.vim' " to not be distracted

call vundle#end()            " required
" load the plugin and indent settings for the detected filetype
filetype plugin indent on    " required
" }

let mapleader = "\<Space>"

syntax enable

set autoread " Automatically reload changes if detected
set encoding=utf8
set history=300

" taken here : https://github.com/thoughtstream/Damian-Conway-s-Vim-Setup/blob/master/.vimrc
"           +--Disable hlsearch while loading viminfo
"           | +--Remember marks for last 50 files
"           | |   +--Remember up to 1000 lines in each register
"           | |   |      +--Remember up to 1MB in each register
"           | |   |      |     +--Remember last 500 search patterns
"           | |   |      |     |     +---Remember last 1000 commands
"           | |   |      |     |     |
"           v v   v      v     v     v
set viminfo=h,'50,<1000,s1000,/500,:1000


" FileType {
" Thorfile, Rakefile, Vagrantfile and Gemfile are Ruby
au BufRead,BufNewFile {Gemfile,Rakefile,Vagrantfile,Thorfile,config.ru,Guardfile} set ft=ruby
" add json syntax highlighting
au BufNewFile,BufRead *.json set ft=javascript
au BufNewFile,BufRead *.hbs set ft=html
" }

" md, markdown, and mk are markdown and define buffer-local preview
au BufRead,BufNewFile *.{md,markdown,mdown,mkd,mkdn,txt} call s:setupWrapping()
function s:setupWrapping()
  set wrap
  set textwidth=0
endfunction

" allow backspacing over everything in insert mode
set backspace=indent,eol,start

" Enable syntastic syntax checking
" let g:syntastic_enable_signs=1

command Vimrc tabnew $MYVIMRC
command Gvimrc tabnew $MYGVIMRC

if has("cryptv")
  set cryptmethod=blowfish
endif

" backup swap {
set nobackup "nobk: in this age of version control, who needs it
set nowritebackup "nowb: don't make a backup before overwriting
set noswapfile "noswf: don't litter swap files everywhere
set directory=/tmp "dir: directory for temp files
" }

" colors {
set background=dark
colorscheme solarized
" }

" CTags ctags {
let Tlist_Auto_Update=1
let Tlist_Ctags_Cmd = '/usr/local/bin/ctags'
" Reload tag
command Rtags execute "!/usr/local/bin/ctags --extra=+f -R *"
nnoremap <C-$> :tnext<CR>
" }

" ctrlp {
"https://github.com/kien/ctrlp.vim
nnoremap <C-b> :CtrlPBuffer<CR>
" }

" configure display {

" cursor position
set ruler

" Status bar
set laststatus=2

set cursorline

" Show (partial) command in the status line
set showcmd

" Suppress mode change messages
set noshowmode

"Display &nbsp and trailing space :
set list listchars=nbsp:•,trail:¬
set iskeyword-=-,? iskeyword+=-,?
" with @ as part of a word
" set iskeyword-=-,?,@-@ iskeyword+=-,?,@-@

" line number
set number
set numberwidth=1
" }

" configure folding {
if &diff
  set foldmethod=diff
else
  set foldmethod=syntax
endif
set foldlevel=6

autocmd BufRead,BufNewFile *.scss,*.less setlocal foldmethod=marker foldmarker={,}
autocmd BufRead,BufNewFile *vimrc setlocal foldmethod=marker foldmarker={,} foldlevel=0
autocmd BufRead,BufNewFile *.{yml,yaml,slim,haml,coffee} set foldmethod=indent
autocmd BufRead,BufNewFile *.rb set foldmethod=syntax
" }

" configure when open large_files {
" http://vim.wikia.com/wiki/Faster_loading_of_large_files
" Protect large files from sourcing and other overhead.
" Files become read only
if !exists("auto_load_large_file")
  let auto_load_large_file = 1
  " Set options:
  " eventignore+=FileType (no syntax highlighting etc
  " assumes FileType always on)
  " noswapfile (save copy of file)
  " bufhidden=unload (save memory when other file is viewed)
  " buftype=nowritefile (is read-only)
  " undolevels=-1 (no undo possible)
  " Large files are > 2M
  let g:LargeFile = 1024 * 1024 * 2
  augroup LargeFile
    autocmd BufReadPre * let f=expand("<afile>") | if getfsize(f) > g:LargeFile | set eventignore+=FileType | setlocal noswapfile bufhidden=unload undolevels=-1 | else | set eventignore-=FileType | endif
    augroup END
  endif
" }

" Explorer netrw {
map <leader>n :Explore<cr>
let g:netrw_liststyle=3
" }

" custom rails mapping {
command Rlang execute "tabnew config/locales/en.yml | vsplit config/locales/fr.yml"
" }

" abbrev rails {
" theise abbrev are for rails
" TODO : add a autocommand for filetype .rb for these abbrev
iab CEF CollectElectricityFrance
iab cef collect_electricity_france
iab CG CollectGeneric
iab cg collect_generic
" }

" For vim-rails {
" help -> :help rails-projection
let g:rails_projections = {
      \ "app/decorators/*_decorator.rb": {
      \   "command": "decorator",
      \   "template":
      \     "class %SDecorator < ApplicationDecorator\nend",
      \   "test": [
      \     "spec/decorators/%s_decorator_spec.rb"
      \   ],
      \   "alternate": 'app/models/%s.rb'
      \ },
      \ "spec/factories/*.rb": {
      \   "command": "factory",
      \   "template":
      \     "FactoryGirl.define do\nfactory :%s, class: %S do\nend\nend",
      \   "test": [
      \     "spec/models/%s_spec.rb"
      \   ],
      \ },
      \ "app/repository/*_repository.rb": {
      \   "command": "repository",
      \   "template":
      \     "class %SRepository\nend",
      \   "test": [
      \     "spec/repository/%s_repository_spec.rb"
      \   ],
      \ },
      \ "app/forms/*_form.rb": {
      \   "command": "form",
      \   "template":
      \     "class %Form\nend",
      \   "test": [
      \     "spec/forms/%s_form_spec.rb"
      \   ],
      \ },
      \ "features/support/*.rb": {"command": "support"},
      \ "features/support/env.rb": {"command": "support"}
      \}
" }

" snipmate {
let g:snippets_dir=globpath(&runtimepath, 'my_snippets')
" }

" tabularize {
" see .vim/after/plugin/my_tabular.vim
" }

" wildignore {
" vim-scripts/gitignore update the list too
" Patter ignore when use the completion in search file
set wildignore+=*.o,*.obj,*~,#*#,*.pyc,*.tar*,*.avi,*.ogg,*.mp3,*.ico
set wildignore+=.git,*.rbc,*.class,.svn,vendor/gems/*
set wildignore+=*/.git/*,*/.hg/*,*/.svn/*
set wildignore+=*/tmp/*,*.so,*.swp,*.zip,*.exe
" }

" Completion {
" Completion like bash
" menuone is to always display menu
set completeopt+=longest,menuone
"tab completion for files
set wildmode=list:longest,full
set dictionary=/Users/tim/.vim/dictionary/francais.txt
set complete-=i " Tim pop says no. So no
" Add dictionnary to <Ctrl-N> " will see if usefull in futur
"set complete-=k complete+=k
" }

" Remember last location in file {
au BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal g'\"" | endif
" }

" map other {
" search for highlighted text
" TODO not working perfectly
vnoremap // y/<C-R>"<CR>

"open the same directory as the current buffer !
nnoremap <leader>e :e <C-R>=expand("%:p:h") . "/" <CR>
"display the same directory as the current buffer !
cnoremap %% <C-R>=expand("%:p:h") . "/" <CR>
" because j rarely used in insert
inoremap j% <C-R>=expand("%:p:h") . "/" <CR>

"taken from https://github.com/carlhuda/janus -> plugin/mappings.vim
nnoremap <F4> :set invpaste<CR>:set paste?<CR>

" format the entire file,
" mf   -> mark line inside f,
" gg=G -> reindent,
" 'f   -> go to current line
nnoremap <leader>fef mfgg=G'f

"display cursor column
nnoremap <leader>col :set invcursorcolumn<CR>
" }

" map surround {

" when on a word, change 'word' to '#{word}' (usefull for ruby)
" Leader a , with a like accolade
nnoremap <Leader>sa diwi#{<C-r>"}<ESC>
nnoremap <Leader>sA diWi#{<C-r>"}<ESC>

nnoremap <Leader>s' diwi'<C-r>"'<ESC>
nnoremap <Leader>s' diWi'<C-r>"'<ESC>

nnoremap <Leader>s" diwi"<C-r>""<ESC>
nnoremap <Leader>s" diWi"<C-r>""<ESC>
" }

" yank copy mapping {

" "*y Copy to 'clipboard registry'
" Work like 'y' but copy in OS clipboard (géniaaaal !!)
" Dont use nmap because we need in vmap
" Don't use noremap because it not understand << Y$ >>
" save in clipboard
vmap Y "*y
" save all line in clipboard
nnoremap YY ^"*y$

" Yank the word on which i am
" i<esc> is used to go back after yank with `^
nnoremap <Leader>y i<esc>lyiw`^
" Yank the word on which i am and put it in the clipboard
" i<esc> is used to go back after yank with `^
nnoremap <Leader>Y i<esc>l"*yiw`^

" Make Y behave like C and D.
" taken from https://github.com/tpope/vim-sensible
nmap Y y$
" }

" paste mapping {
" If you visually select something and hit paste
" that thing gets yanked into your buffer. This
" generally is annoying when you're copying one item
" and repeatedly pasting it. This changes the paste
" command in visual mode so that it doesn't overwrite
" whatever is in your paste buffer.
" taken here
" http://yanpritzker.com/2012/01/20/the-cleanest-vimrc-youve-ever-seen/
vnoremap p "_dp
vnoremap P "_dP
nnoremap <leader>p "_ciw<C-r>"<esc>
"http://vim.wikia.com/wiki/Selecting_your_pasted_text
nnoremap <expr> gp '`[' . strpart(getregtype(), 0, 1) . '`]'
" }

" delete mapping {
"when use x, do not send to test register <""> but send to black hole
"register "_ (ie void, or /dev/null or divide by 0...)
nnoremap x "_x
nnoremap X "_X
vnoremap x "_x
vnoremap X "_X

" delete the word on which i am
nmap <Leader>d diw
" delete the word on which i am and put it in the clipboard
nmap <Leader>D "*diw

" Remove the word under the cursor
" Do not save the word in any register
" and go in insert mode
nmap <Leader>r "_ciw
" }

" Move visual block {
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv
" }
"
" mouse {
if has('mouse')
  set mouse=a
endif

"Fix bug : mouse before cannot go to 220 char to the right
"This fix it
"https://code.google.com/p/iterm2/issues/detail?can=2&start=0&num=100&q=&colspec=ID%20Type%20Status%20Priority%20Milestone%20Owner%20Summary&groupby=&sort=&id=1830
if has('mouse_sgr')
  set ttymouse=sgr
endif
" }

" run ruby {
" taken inside the demo video https://www.destroyallsoftware.com/screencasts
" it run the current ruby file and display result
map <leader>l :w\|:!ruby %<CR>
" }

" scroll {
"show three line before up and down => MAGIC
set scrolloff=3
"show 15 char before and after cursor => MAGIC
set sidescrolloff=15
set sidescroll=1
" }

" search {
set incsearch "is: automatically begins searching as you type
set noignorecase "ic: no ignores case
set smartcase "scs: ignores ignorecase when pattern contains uppercase characters
set hlsearch "hls: highlights search results; ctrl-n or :noh to unhighlight
set gdefault "gd: Substitute all matches in a line by default no need s/a/b/g
set nowrapscan "Stop search at end of the file

" when on a word, it search for this word
" and replace with the specified value
nnoremap <Leader>sr yiw:%s/<C-r>"//c<Left><Left>
vnoremap <Leader>sr y:%s/<C-r>"//c<Left><Left>
nnoremap <Leader>SR yiW:%s/<C-r>"//c<Left><Left>
map <leader>a :AgFromSearch!<cr>
" }

" spell {
" Use vim spell:
" zg Add word under the cursor as a good word_to_search
" zw Mark the word as a wrong (bad) word.wrong
" z= For the word under/after the cursor suggest correctly
" spelled words.

" Spell Check, used to togle between no spell and language. To add a language
" juste download a language from here : http://ftp.vim.org/vim/runtime/spell/
" , and copy paste in ~/.vim/spell/
" let b:myLang=0
" let g:myLangList=["nospell","fr","en"]
" function! ToggleSpell()
"   let b:myLang=b:myLang+1
"   if b:myLang>=len(g:myLangList) | let b:myLang=0 | endif
"   if b:myLang==0
"     setlocal nospell
"   else
    " execute "setlocal spell spelllang=".get(g:myLangList, b:myLang)
"   endif
"   echo "spell checking language:" g:myLangList[b:myLang]
" endfunction

" nmap <F7> :call ToggleSpell()<CR>

"spell check when writing commit logs
" if has("autocmd")
"   autocmd filetype svn,*commit* set spell
" endif
" }

" Tips and tricks {

"## g with norm
"http://briancarper.net/blog/165/
"example:
":g/\d/norm o999

"## Encoding
"http://stackoverflow.com/questions/778069/how-can-i-change-a-files-encoding-with-vim
"Notice that there is a difference between
"set encoding
"and
"set fileencoding

"Donc pour modifier un fichier, faire :
"set fileencoding=utf-8
":wq
"
" ## use hexa editor
"Use vim as HEXA editor :
"open your file as usual
"[esc] :%!xxd
"<now editing mycat in hex>
"<find some innocuous string or rcsid>
"<change the values on the hex side>
"[esc] :%!xxd -r
"[esc] :wq!
"
" ## To add an abbrev for a specifiq filetype :
" autocmd FileType javascript :iabbrev <buffer> cl console.log();<left><left>

" }

"underscorize {
"This fonction convert any upper case to _letter
"example : HiTheirLol -> hi_their_lol

"help from here
"http://stackoverflow.com/questions/10572996/passing-command-range-to-a-function
command -range Underscore <line1>,<line2>call UnderscoreAll()

" Idea of function taken here :
" http://www.ibm.com/developerworks/linux/library/l-vim-script-2/
function UnderscoreAll() range
  "Step through each line in the range...
  for linenum in range(a:firstline, a:lastline)
    let curr_line   = getline(linenum)
    " from ruby on rails
    " http://api.rubyonrails.org/classes/ActiveSupport/Inflector.html#method-i-underscore
    let replacement = substitute(curr_line  , '\([A-Z\d]+\)\([A-Z][a-z]\)', '\1_\2', 'g')
    let replacement = substitute(replacement, '\([a-z\d]\)\([A-Z]\)'      , '\1_\2', 'g')
    let replacement = tolower(replacement)
    call setline(linenum, replacement)
  endfor
endfunction
" }

" up down movement {
" when go to left at a start of line, it goes to the end of previous
set whichwrap+=<,>,h,l,[,]

" Map the arrow keys to be based on display lines, not physical lines
map <Down> gj
map <Up> gk
" }

" Whitespace space tab stuff {
set nowrap
set tabstop=2 "ts: number of spaces that a tab renders as
set shiftwidth=2 "sw: number of spaces to use for autoindent
set softtabstop=2 "sts: number of spaces that tabs insert
set smarttab "sta: helps with backspacing because of expandtab
set expandtab "et: uses spaces instead of tab characters

" make uses real tabs
au FileType make set noexpandtab
" make Python follow PEP8 ( http://www.python.org/dev/peps/pep-0008/ )
au FileType python set softtabstop=4 tabstop=4 shiftwidth=4 textwidth=79 noexpandtab

" Delete all whitespace in end of line
if has("autocmd")
  autocmd BufWritePre * :%s/\s\+$//e
endif
" }

" save session
" need to do vim -S to open it
nnoremap <leader>s :mksession<CR>

nnoremap <leader>w :w<CR>
nnoremap <leader>o o<esc>
nnoremap <leader>O O<esc>

" monve in window stuff {
" ----------------------------------------------------------------------------
" move to the window in the direction shown, or create a new split in that
" direction
" ----------------------------------------------------------------------------
func! WinMove(key)
    let t:curwin = winnr()
    exec "wincmd ".a:key
    if (t:curwin == winnr())
        if (match(a:key,'[jk]'))
            wincmd v
        else
            wincmd s
        endif
        exec "wincmd ".a:key
    endif
endfu

nnoremap <silent> <C-h> :call WinMove('h')<cr>
nnoremap <silent> <C-j> :call WinMove('j')<cr>
nnoremap <silent> <C-k> :call WinMove('k')<cr>
nnoremap <silent> <C-l> :call WinMove('l')<cr>

" Switch directly to the new splitted window
nnoremap <C-w>v :rightbelow vsplit<CR>
nnoremap <C-w>s :split<CR> :wincmd j<CR>

" }
"
set ttyfast "tf: improves redrawing for newer computers
set lazyredraw "lz: will not redraw the screen while running macros (goes faster)
